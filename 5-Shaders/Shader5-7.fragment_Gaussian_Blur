#version 330 core
out vec4 FragColor;
  
in vec2 TexCoords;

uniform sampler2D image;
  
uniform bool horizontal;	//水平
uniform float weight[5] = float[] (0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216);	//这些数值的和加起来仅0.6+

void main()
{             
    vec2 tex_offset = 1.0 / textureSize(image, 0); // 获取单个纹理的大小
    vec3 result = texture(image, TexCoords).rgb * weight[0]; // 该片段本身的贡献度
    if(horizontal)
    {
        for(int i = 1; i < 5; ++i)
        {
			//依次向左向右分别采集5次，获取其对应贡献度下的纹理值
            result += texture(image, TexCoords + vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
            result += texture(image, TexCoords - vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
        }
    }
    else
    {
        for(int i = 1; i < 5; ++i)
        {
			//依次向上向下分别采集5次，获取其对应贡献度下的纹理值
            result += texture(image, TexCoords + vec2(0.0, tex_offset.y * i)).rgb * weight[i];
            result += texture(image, TexCoords - vec2(0.0, tex_offset.y * i)).rgb * weight[i];
        }
    }
    FragColor = vec4(result, 1.0);
}